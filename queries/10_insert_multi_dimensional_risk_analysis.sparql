PREFIX wu: <http://www.semanticweb.org/zerda/ontologies/2025/6/ontology_painkiller/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

INSERT {
    ?drug wu:hasGeoAnalysis [
        a wu:GeoAnalysis ;
        wu:geoInCountry ?country ;
        wu:geoReportCount ?countryCount ;
        wu:geoAveragePatientAge ?avgAge ;
        wu:geoCountryContributionPercentage ?countryContributionPercentage ;
        wu:geoDistributedFatalPercentage ?distributedFatalPercentage
    ] .
}
WHERE {
    {
        SELECT ?drug ?country (COUNT(?report) AS ?countryCount) (ROUND(AVG(xsd:integer(?age))) AS ?avgAge)
        WHERE {
            ?report wu:reportConcernsDrug ?drug ;
                    wu:reportCountryCode ?country ;
                    wu:reportInvolvesPatient ?patient .
            ?patient wu:patientAge ?age .
        }
        GROUP BY ?drug ?country
    }
    {
        SELECT ?drug (COUNT(?report) as ?totalFatalForDrug)
        WHERE {
            ?report wu:reportConcernsDrug ?drug .
        }
        GROUP BY ?drug
    }
    ?drug wu:fatalReportPercentage ?fatalPercentage .

    BIND(ROUND((?countryCount / ?totalFatalForDrug) * 100) AS ?countryContributionPercentage)
    BIND(ROUND((?fatalPercentage * ?countryContributionPercentage / 100.0) * 100) / 100.0 AS ?distributedFatalPercentage)
}